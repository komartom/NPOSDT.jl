{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "using BlackBoxOptim, DelimitedFiles"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "tree (generic function with 3 methods)"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function load_dataset(path)\n",
    "    XY = readdlm(path, ';', skipstart=1)\n",
    "    [(label=XY[rr,end]==1, sample=convert(Vector{Bool}, XY[rr,1:end-1])) for rr in 1:size(XY,1)]\n",
    "end\n",
    "\n",
    "\n",
    "function leaf(tree, sample)\n",
    "    index = 1\n",
    "    while index <= length(tree)\n",
    "        feature = tree[index]\n",
    "        if feature == 0\n",
    "            break\n",
    "        else\n",
    "            index = sample[feature] ? 2*index+1 : 2*index\n",
    "        end\n",
    "    end\n",
    "    index\n",
    "end\n",
    "\n",
    "\n",
    "function accuracy(tree, dataset)\n",
    "    \n",
    "    N = (length(tree)+1)*2\n",
    "    positives = zeros(Int, N)\n",
    "    negatives = zeros(Int, N)\n",
    "\n",
    "    for (label, sample) in dataset\n",
    "        if label\n",
    "            positives[leaf(tree, sample)] += 1\n",
    "        else\n",
    "            negatives[leaf(tree, sample)] += 1\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    correct = 0\n",
    "    for (pp, nn) in zip(positives, negatives)\n",
    "        correct += pp > nn ? pp : nn\n",
    "    end\n",
    "    \n",
    "    correct / length(dataset)\n",
    "    \n",
    "end\n",
    "\n",
    "\n",
    "nleaves(tree, index=1) = (index>length(tree)||tree[index]==0) ? 1.0 : nleaves(tree,2*index)+nleaves(tree,2*index+1)\n",
    "\n",
    "\n",
    "function tree(dataset, λ=0.005, max_tree_depth=5)\n",
    "    \n",
    "    nfeatures = length(dataset[1].sample)\n",
    "    \n",
    "    fitness(tree) = (1.0-accuracy(tree, dataset), nleaves(tree))\n",
    "    \n",
    "    fitness_scalar(fitness) = fitness[1] + λ*fitness[2]\n",
    "    \n",
    "    multirun = [\n",
    "        bboptimize(\n",
    "            tree->fitness(round.(Int, tree));\n",
    "            SearchRange = (0.0, Float64(nfeatures)), \n",
    "            NumDimensions = 2^max_tree_depth-1,\n",
    "            MaxTime = 5,\n",
    "            TraceMode = :silent,\n",
    "            Method = :borg_moea,\n",
    "            ϵ = 0.0001, τ = 0.1,\n",
    "            PopulationSize = 500,\n",
    "            MaxStepsWithoutProgress = 10^6,\n",
    "            FitnessScheme=ParetoFitnessScheme{2}(is_minimizing=true, aggregator=fitness_scalar)\n",
    "        ) for run in 1:24]\n",
    "    \n",
    "    best_run = sort(multirun, by=run->fitness_scalar(best_fitness(run)))[1]\n",
    "    \n",
    "    return round.(Int, best_candidate(best_run))\n",
    "end "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [],
   "source": [
    "dataset = load_dataset(\"./compas-binary.csv\");"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "125.518996 seconds (62.30 M allocations: 7.741 GiB, 1.22% gc time)\n"
     ]
    }
   ],
   "source": [
    "@time best_tree = tree(dataset);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "[12, 4, 0, 0, 11, 8, 0, 6, 2, 8, 0, 1, 1, 0, 6, 5, 1, 11, 1, 0, 0, 9, 12, 7, 6, 5, 9, 6, 11, 4, 9]\n"
     ]
    }
   ],
   "source": [
    "println(best_tree)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Leaves: 5.0\n",
      "Accuracy: 0.669031417402635\n"
     ]
    }
   ],
   "source": [
    "println(\"Leaves: \", nleaves(best_tree))\n",
    "println(\"Accuracy: \", accuracy(best_tree, dataset))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Leaves: 5.0\n",
      "Accuracy: 0.669031417402635\n"
     ]
    }
   ],
   "source": [
    "# Optimal decision tree generated by OSDT algorithm:\n",
    "# Hu, Xiyang, Cynthia Rudin, and Margo Seltzer. \n",
    "# Optimal sparse decision trees (Figure 4).\n",
    "# Advances in Neural Information Processing Systems. 2019.\n",
    "\n",
    "optimal_tree = [12, 4, 0, 0, 11, 0, 0, 0, 0, 8, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]\n",
    "println(\"Leaves: \", nleaves(optimal_tree))\n",
    "println(\"Accuracy: \", accuracy(optimal_tree, dataset))"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.2.0",
   "language": "julia",
   "name": "julia-1.2"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.2.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
